apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "org.sonarqube"
apply plugin: 'net.researchgate.release'
apply plugin: 'spring-boot'
apply plugin: 'war'
apply plugin: 'org.sonarqube'
// To embed into the release process. './gradlew' install will be called.
apply plugin: 'maven'

// Configure the maven repository deployment
install {
    repositories.mavenInstaller {
            // Set the version
            pom.version = connectorIaaSVersion
            // Set the group/namespace for the maven repository deployment.
            pom.groupId = 'org.ow2.proactive'
    }
}

repositories {
	mavenCentral()
    jcenter()
}

buildscript {
  repositories { 
  	maven {
      url "https://plugins.gradle.org/m2/"
    }
  	jcenter() 
  	}
  dependencies {
  	classpath "org.sonarqube.gradle:gradle-sonarqube-plugin:1.0"
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    classpath 'org.postgresql:postgresql:9.3-1102-jdbc41'  
    classpath 'org.ajoberstar:gradle-jacoco:0.3.0'
    classpath 'net.researchgate:gradle-release:2.2.1'
      classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:1.2"
      classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE'
  }
}

mainClassName = 'org.ow2.proactive.connector.iaas.ConnectorIaaSApp'

springBoot {
    mainClass = mainClassName
}

// version is not set to make the war filename short and nice
war {
    baseName = 'connector-iaas'
}

jar {
    baseName = 'connector-iaas'
    // Version is define in gradle.properties
    version =  connectorIaaSVersion
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Class-Path': "."
    }
}

shadowJar {
  mergeServiceFiles()
}

def jdkHome = System.getenv("JAVA_HOME")

release {
    preTagCommitMessage = '[ci skip][Gradle Release Plugin] - pre tag commit: '
    tagCommitMessage = '[ci skip][Gradle Release Plugin] - creating tag: '
    newVersionCommitMessage = '[ci skip][Gradle Release Plugin] - new version commit: '      
}

configurations {
    // Those modules are excluded to avoid clashes when embedded inside the ProActive Scheduler
    all*.exclude module: 'spring-boot-starter-logging'
    all*.exclude module: 'logback-core'
    all*.exclude module: 'logback-classic'
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

dependencies {
    // Lombok plugin to remove boilerplate code from project
    compile 'org.projectlombok:lombok:1.16.6'
    // Guava allows immutable classes/objects
    compile 'com.google.guava:guava:19.0'
    // Support for easy class to Json convertion and vice verca
    compile group: 'com.aol.microservices', name:'micro-jackson-configuration', version:'0.79.5'
    // For using the rest jersey annotations
    compile 'org.springframework.boot:spring-boot-starter-jersey:1.3.1.RELEASE'
    // The basic spring boot web 'packet' -- tomcat is excluded because this project will be embedded
    // in ProActive Scheduler
    compile('org.springframework.boot:spring-boot-starter-web:1.3.1.RELEASE') {
        exclude module: "spring-boot-starter-tomcat"
    }
    // This allows log4j logging for spring boot. This dependency is needed for standalaone but will
    // clash with the ProActive Scheduler dependencies when included. It is excluded in configurations above.
    compile 'org.springframework.boot:spring-boot-starter-log4j2:1.3.1.RELEASE'
    // Provides a standalone jetty server. It is providedCompile to be placed inside the provided folder
    // in the war/jar. When a war is loaded, the jars in the provided folder are not loaded.
    providedCompile 'org.springframework.boot:spring-boot-starter-jetty:1.3.1.RELEASE'
    // Jcloud dependencies
    compile ('org.apache.stratos:jclouds-compute:1.9.1'){
        exclude module : 'jsr311-api'
    }
    compile ('org.apache.jclouds.api:openstack-nova:1.9.1'){
        exclude module : 'jsr311-api'
    }
    compile ('org.apache.jclouds.provider:aws-ec2:1.9.1'){
        exclude module : 'jsr311-api'
    }
    compile ('org.apache.jclouds.labs:docker:1.9.1'){
        exclude module : 'jsr311-api'
    }
    // Json project
    compile 'org.json:json:20151123'

    // Imports for JUnit tests
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.28'

    testCompile 'org.springframework.boot:spring-boot-starter-test:1.3.1.RELEASE'
    testCompile 'com.jayway.restassured:rest-assured:2.8.0'


	compile files("$jdkHome/lib/tools.jar")
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version:'1.1'
    testCompile ('junit:junit:4.12') {
            exclude module : 'hamcrest'
            exclude module : 'hamcrest-core'
        }
    testCompile group: 'org.mockito', name: 'mockito-all', version:'1.9.5'
	testCompile 'org.springframework:spring-test:2.5'
}


sourceSets {
	test {
        java.srcDir file('src/test/java')
        resources.srcDir file('src/test/resources')
    }
    integTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
    }
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
}


dependencies {
    integTestCompile sourceSets.main.output
    integTestCompile configurations.testCompile
    integTestCompile sourceSets.test.output
    integTestRuntime configurations.testRuntime
}

task local {
    run { systemProperty "spring.profiles.active", "local" }
}

// Configure sonarqube plugin.
// Username and password need to be added through the command line. Add username with
// -Dsonar.jdbc.username=[username] and password with
// -Dsonar.jdbc.password=[password] to the './gradlew sonarqube' command.

// For sonarqube github access sonar.github.repository=ow2-proactive/connector-iaas
// is added. Following proerties need to be added
// from commandline to make github pull request reports working.
// -Dsonar.github.pullRequest=${ghprbPullId} The current pull request id. ${ghprbPullId} is a
// propagated jenkins variable, from the github pull request plugin.
// sonar.github.login=[github-username] github login to write comments
// sonar.github.oauth=[oauth-token] github oauth token to write comments on pull reqeust
// sonar.issuesReport.console.enable=true to have a report on the commandline (for jenkins)
// sonar.analysis.mode=preview the preview mode does not write the result into the database and only analyzes
// files modified in the current pull request.
sonarqube {
    properties {
        property "sonar.projectName", "connector-iaas"
        property "sonar.host.url", "http://master.ci.activeeon.com:9000"
        property "sonar.jdbc.url", "jdbc:postgresql://master.ci.activeeon.com:5432/sonar"
        property "sonar.projectVersion", connectorIaaSVersion
        // For github plugin
        property "sonar.github.repository", "ow2-proactive/connector-iaas"
    }
}

allprojects {
    apply plugin: 'jacoco'  
    
	jacoco {
	    toolVersion = '0.7.1.201405082137'
	}
	
	jacocoTestReport {
	    additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
	    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
	    classDirectories =  files(sourceSets.main.output)
	    reports {
	        html.enabled = true
	        xml.enabled = true
	        csv.enabled = false
	    }
	}
	
	
}
